#!/bin/zsh
# wallpaper setter script for X
emulate -LR zsh;
typeset c o O;
typeset -i n;
typeset -a opt dir file;
opt=(
  --bg-center
  --bg-fill
  --bg-max
  --bg-scale
  --bg-tile
);
c=~/im/wp; # starting direcotry
cd ${c};
O=$(pcre2grep -o1 "feh --no-fehbg (.+?) '.+'" ~/.fehbg);

while { true } {
  [[ -d ${c} ]] && {
    dir=(*(/D-N));
    file=(*(.D-N));
    n=${#file};
  };
  c=$(printf '%s\n' .. ${dir} ${file} rand opts | dmenu -p "$(pwd) ") || break; 
  if [[ ${c} == opts ]] {
    o=${$(dmenu <<< ${(F)opt}):*opt};
    feh ${o:-${O}} "$(pcre2grep -o1 "'(.*)'" ~/.fehbg)";
  } elif [[ ${c} == rand ]] {
    feh ${o:-${O}} ${file[RANDOM % ${n} + 1]};
  } elif [[ -d ${c} ]] {
    c=${c:a}; cd ${c};
  } elif [[ -f ${c} ]] {
    feh ${o:-${O}} ${c} &;
  };
};

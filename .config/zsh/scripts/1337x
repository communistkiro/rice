#!/bin/zsh
[[ -t 0 ]] || return 1;
# { which zsh && which pup && which curl && which uritool } &>/dev/null || return 2;
# ping -c 1 1.1.1.1 &>/dev/null || return 3;
emulate -LR zsh;
setopt pipe_fail extended_glob;
typeset query base cat t n; 
typeset -i results pages c q lr ls ll lc ld lt; 
typeset -a cats title size url category date hash seed leech;
n=$'\n'; 
t=$(mktemp);
quit () { rm ${t} &>/dev/null; exit ${1:-0} }
export FZF_DEFAULT_OPTS="-i +m --reverse --ansi --bind=\[:up,\':down --tiebreak=begin";
for base (1337x.to 1337x.tw 1337x.st 1377x.one 1337x.gd x1337x.eu x1337x.se) {
  ping -c 1 ${base} &>/dev/null && { base=https://${base}; break };
} || quit 3;

query="${${@}:-$(read -er "?${n}> ")}";
[[ ! ${query} =~ '^\s*$' ]] || quit 4;
query=$(uritool query escape ${query});

cats=(Anime Apps Documentaries Games Movies Music Other TV XXX);
cat=${$(fzy -p 'Select category or search all: ' -l ${#cats} <<< ${(F)cats}):*cats};
[[ -z ${cat} ]] && query=/search/${query}/ || query=/category-search/${query}/${cat}/;

curl -sfLH User-Agent: -o ${t} ${base}${query}1/;

[[ $(pup -p -f ${t} 'body div div div p text{}') =~ 'No results' ]] && { printf 'No results\n'; quit };

pages=${${$(pup -f ${t} '.last a attr{href}')%/}##*/};
if ((pages == 0)) {
  pages=1;
} elif ((pages > 4)) && [[ $(read -seq "?${n}${pages} pages, show all [y/*]?") == n ]] {
  read -r "q?${n}Input number of pages to display> ";
  ((q > 0 && q <= pages)) && pages=${q} || { printf 'Invalid selection; showing first 5 pages.\n'; pages=5 };
};

for q ( {1..${pages}} ) {
  title+=(${(f)"$(   pup -p -f ${t} 'td.name a text{}')"});
  seed+=(${(f)"$(    pup -p -f ${t} 'td.seeds text{}')"});
  leech+=(${(f)"$(   pup -p -f ${t} 'td.leeches text{}')"});
  size+=(${(f)"$(    pup -p -f ${t} 'td.size json{}' | jq -r '.[].text')"});
  url+=(${(f)"$(     pup -p -f ${t} 'td a[href^=/torrent/] attr{href}')"});
  category+=(${(f)"$(pup -p -f ${t} 'td.name .icon i attr{class}' | cut -d - -f 2-)"});
  date+=(${(f)"$(    pup -p -f ${t} 'td.coll-date text{}')"});
  curl -sfLH User-Agent: -o ${t} ${base}${query}$((q + 1))/;
};

results=${#title};
for q ( {1..${results}} ) {
  ((${(c)#title[q]}    > lt)) && lt=${(c)#title[q]};
  ((${(c)#category[q]} > lc)) && lc=${(c)#category[q]};
  ((${(c)#date[q]}     > ld)) && ld=${(c)#date[q]};
};

lr=${(c)#${results}};
ls=${(c)#${${(nO)seed}[1]}};
ll=${(c)#${${(nO)leech}[1]}};
# lt=$((COLUMNS - lr - 2 - 1 - 9 - 2 - 9 - 2 - ls - 1 - ll - 2 - 14));

while { true } {
  c=$(for q ( {1..${results}} ) {
    printf "%b%.${lr}d  %b%-${lt}.${lt}s %b%-${lc}.${lc}s  %b%9.9s  %b%.${ls}d%b/%b%.${ll}d  %b%${ld}.${ld}s\n" \
      '\033[0m'    $q \
      '\033[0;1m'  ${title[q]:- } \
      '\033[33;1m' ${category[q]:- } \
      '\033[34;1m' ${size[q]:- } \
      '\033[32;1m' ${seed[q]:- } \
      '\033[37;1m' \
      '\033[31;1m' ${leech[q]:- } \
      '\033[36;1m' ${date[q]:- };
  } | fzf | cut -f 1 -d ' ') || break;
  ((c > 0 && c <= ${results})) && {
    curl -sfLH User-Agent: -o ${t} ${base}${(Q)url[c]};
    hash+=($(pup -p -f ${t} 'a[href^=magnet] attr{href}'));
    [[ -n ${hash[-1]} ]] || hash[-1]=$(pup -p -f ${t} '.infohash-box span text{}');
  };
};

((${#hash} > 0)) && { qbittorrent ${(u)hash} &>/dev/null & disown };
#((${#hash} > 0)) && for q ( {1..${#hash}} ) {
#  printf '%s\n' d10:magnet-uri${#hash[q]}:${hash[q]}e > ~/Downloads/rtorrent/watch/start/meta-${${hash[q]%%&*}##*:}.torrent;
#};
quit;

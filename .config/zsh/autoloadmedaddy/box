#!/bin/zsh
function box () {
((# == 0)) && return 0;
local a d h B T H tl tr bl br th bh lv rv r w; integer s; s=1;
while getopts 's:h:B:T:H:' a; do case $a in
  (s) # line style
    ((OPTARG >= 1 && OPTARG < 5)) && s=$OPTARG;;
  (h) # header
    h=$OPTARG;;
  (B) # box color
    B="\033[;3$(pcre2grep -o1 '([0-7])' <<< $OPTARG || <<< 7)";
    [[ $OPTARG =~ i ]] && B="${B};7";
    [[ $OPTARG =~ b ]] && B="${B};1";
    B="${B}m";;
  (T) # text color
    T="\033[;3$(pcre2grep -o1 '([0-7])' <<< $OPTARG || <<< 7)";
    [[ $OPTARG =~ i ]] && T="${T};7";
    [[ $OPTARG =~ b ]] && T="${T};1";
    T="${T}m";;
  (H) # header color
    H="\033[;3$(pcre2grep -o1 '([0-7])' <<< $OPTARG || <<< 7)";
    [[ $OPTARG =~ i ]] && H="${H};7";
    [[ $OPTARG =~ b ]] && H="${H};1";
    H="${H}m";;
(*) less -F << HEREDOC # help wanted.
NAME
    box - enclose strings in box

SYNOPSIS
    box [-s 1--4] [-BTH [0-7]ib] [-h header] [string(s)]

DESCRIPTION
    box is a zsh script that encloses one or more strings, extending to 4 above the lengthiest 
    string, header included. The box's style is given by '-s', and may be thick, thin, double, 
    or dotted; may have a title; and may color the box's line, text, and header in different 
    colors according to ANSI C escape codes. The color is a 1--3-character sequence of a number 
    0--7,'b' for bold color, and 'i' to switch fore- and background colors. The number 
    corresponding to the colors black, red, green, yellow, blue, magenta, cyan, and white. 
    Defaults to thick lines and white for box, text, and header. Don't use = for options 
    accepting arguments.

EXAMPLES
    box -h header 'This is a sentence on the first row' 'This is a second row'

    a=Zz; box -s 3 -T 3b -B b2 -H 5i -h 'looooooooooooooooong snooze' ${(pr.16..$a.l.8...)a} "padding works, too"

HEREDOC
return 1;; esac; done;

tl=(┏ ┌ ╔ ⡏); tl=${tl[$s]}; # top left
bl=(┗ └ ╚ ⣇); bl=${bl[$s]}; # bottom left
tr=(┓ ┐ ╗ ⢹); tr=${tr[$s]}; # top right
br=(┛ ┘ ╝ ⣸); br=${br[$s]}; # bottom right
th=(━ ─ ═ ⠉); th=${th[$s]}; # top horizontal
bh=(━ ─ ═ ⣀); bh=${bh[$s]}; # bottom horizontal
lv=(┃ │ ║ ⡇); lv=${lv[$s]}; # left vertical
rv=(┃ │ ║ ⢸); rv=${rv[$s]}; # right vertical
r='\033[;0m';
# box inside width
w=0; for a in $@[OPTIND,-1]; do ((${(c)#a} > w)) && w=${(c)#a}; done; 
((${+h:+1} && $#h > w)) && w=$#h && ((w+=2)); ((w+=2));

((w + 2 > COLUMNS)) && {
  # FIXME; IF HEADER MULTI ROW OR LONGER THAN COLUMNS ... ELSE
  ((${+h:+1})) && printf '%b%s%s%b%s%b%s%s\n' $B $tl ${(pr.(COLUMNS-2-$#h)/2..$th.)th} $r$H $h $r$B ${(pr.(COLUMNS-2-$#h)/2+$#h+COLUMNS-2-($#h+COLUMNS-2)/2*2..$th.)th} $tr || printf '%b%s%s%s\n' $B $tl ${(pr.COLUMNS-3..$th.)th} $tr;
  for a in $@[OPTIND,-1]; do 
    for d in ${(f)"$(fmt -w $COLUMNS <<< $a)"}; do
      printf '%s %b%s%b %s\n' $lv $r$T ${(r.COLUMNS-4.. .)d} $r$B $rv; 
    done;
  done;
  printf '%s%s%s%b\n' $bl ${(pr.COLUMNS-2..$bh.)bh} $br $r;
} || {
  ((${+h:+1})) && printf '%b%s%s%b%s%b%s%s\n' $B $tl ${(pr.(w-$#h)/2..$th.)th} $r$H $h $r$B ${(pr.(w-$#h)/2+$#h+w-($#h+w)/2*2..$th.)th} $tr || printf '%b%s%s%s\n' $B $tl ${(pr.w-1..$th.)th} $tr;
  for a in $@[OPTIND,-1]; do printf '%s %b%s%b %s\n' $lv $r$T ${(r.w-2.. .)a} $r$B $rv; done;
  printf '%s%s%s%b\n' $bl ${(pr.w..$bh.)bh} $br $r;
};
}

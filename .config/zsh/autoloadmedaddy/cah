#!/bin/zsh
# a terminal solo cards against humanity experience using https://github.com/crhallberg/json-against-humanity
function cah () {
typeset file choice
typeset -i white black card query;
typeset -a packname packnum nwhite nblack;
file=~/games/json-against-humanity/cah-all-compact.json;

white=$(jq '.white[]' $file | wc -l);
black=$(jq '.black[].text' $file | wc -l);

packname=(${(f)"$(jq -r ".packs[${(j.,.)packname}].name" $file)"});
packnum=($(seq -w 0 $(jq '.packs[].name' $file | wc -l)));
packnum[-1]=();

for choice in ${(f)"$(printf '%s %s\n' ${packnum:^packname} | fzf -i -m --tiebreak=begin,end,index --reverse --no-info --border=horizontal --header='Packs to remove: ' --bind 'alt-c:clear-selection' --bind 'alt-v:select-all')"}; do
  nwhite+=(${(f)"$(jq -r ".packs[${choice%% *}].white[]" $file)"});
  nblack+=(${(f)"$(jq -r ".packs[${choice%% *}].black[]" $file)"});
done;

while true; do
  for card in {1..10}; do until [[ -n ${hand[card]:|nwhite} ]]; do hand[card]=$((RANDOM % white)); done; done;
  until [[ -n ${query:|nblack} ]]; do query=$((RANDOM % black)); done;
  clear;
  jq ".white[${(j.,.)hand}]" $file | fzf -i -m --no-info --reverse --border=horizontal --tiebreak=length,end --height=15 --header="$(jq -r ".black[$query].text" $file)" || break;
  hand[1,-1]=();
  query=;
done;
}